#include "imports/stdlib.fc";

const UPDATE = 0x9df10277;
const CLAIM = 0xbb4be234;

() recv_internal() impure {

}
;;24: 142,246,542
() recv_external(slice in_msg) impure {
    int op = in_msg~load_uint(32);

    var ds = get_data().begin_parse(); ;; get data from storage and convert it into a slice to be able to read values
    var (public_key, execution_time, receiver, seqno) = (ds~load_uint(256), ds~load_uint(32),ds~load_msg_addr(), ds~load_uint(32)); ;; read values from storage

    if(op == UPDATE) {
        var (query_id, signature) = (in_msg~load_uint(64), in_msg~load_bits(512)); ;; get signature from the message body
        ;; var left_in_msg = in_msg;
        var c = in_msg~load_ref();
        var cs = c.begin_parse();
        var mmm = cs;
        var (locked_for, new_seqno) = (cs~load_uint(32), cs~load_uint(32));  ;; get rest values from the message body
        int now_time = now();
        int new_for_time = now_time + locked_for;
        throw_if(119, new_seqno != seqno + 1);
        throw_unless(120, check_signature(slice_hash(mmm), signature, public_key));
        throw_if(121, locked_for <= 0);
        throw_if(122, execution_time < now_time);
        throw_if(123, new_for_time < execution_time);
        accept_message();
        set_data(begin_cell()
            .store_uint(public_key, 256)
            .store_uint(new_for_time, 32)
            .store_slice(receiver) ;; destination address
            .store_uint(seqno + 1, 32)
           .end_cell());
        return ();  
    }

    if(op == CLAIM) {
        throw_if(124, execution_time > now());
        accept_message();
        cell msg = begin_cell()
            .store_uint(0x18, 6) ;; flags
            .store_slice(receiver) ;; destination address
            .store_coins(0) ;; we don't care about this value right now
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
        .end_cell();
    
        send_raw_message(msg, 128); ;; mode = 128 is use
        return ();  
    }

    return ();
}

int get_seqno() method_id {
    ;; slice ds = get_data().begin_parse().skip_bits(256 + 32 + 267);
    ;; var receiver = ds~load_msg_addr(); ;; read values from storage
    return get_data().begin_parse().skip_bits(256 + 32 + 267).preload_uint(32);
}
int get_execution_time() method_id {
    int total = get_data().begin_parse().skip_bits(256).preload_uint(32);
    return total;
}
